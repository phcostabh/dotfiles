" Base configuration {
   set timeoutlen=300                                 " mapping timeout
   set ttimeoutlen=50                                 " keycode timeout
   set mouse=a                                        " enable mouse
   set mousehide                                      " hide when characters are typed
   set history=1000                                   " number of command lines to remember
   set ttyfast                                        " assume fast terminal connection
   set viewoptions=folds,options,cursor,unix,slash    " unix/windows compatibility
   set encoding=utf-8                                 " set encoding for text
   if exists('$TMUX')
      set clipboard=
   else
      set clipboard=unnamed                           " sync with OS clipboard
   endif
   set hidden                                         " allow buffer switching without saving
   set autoread                                       " auto reload if file saved externally
   set nrformats-=octal                               " always assume decimal numbers
   set showcmd
   set whichwrap+=<,>,h,l,[,]
   set tags=tags;/
   set showfulltag
   set modeline
   set modelines=5

                                                      " whitespace
   set backspace=indent,eol,start                     " allow backspacing everything in insert mode
   set autoindent                                     " automatically indent to match adjacent lines
   set expandtab                                      " spaces instead of tabs
   set smarttab                                       " use shiftwidth to enter tabs
   let &tabstop=4                                     " number of spaces per tab for display
   let &softtabstop=4                                 " number of spaces per tab in insert mode
   let &shiftwidth=4                                  " number of spaces when indenting
   set listchars=tab:│\ ,trail:•,extends:❯,precedes:❮
   set shiftround
   set linebreak
   let &showbreak='↪ '

   set wildignore+=*/.git/*,*/.hg/*,*/.svn/*,*/.idea/*,*/.DS_Store
   set wildignore+=*.jpeg,*.jpg,*.gif                 " Ingnores also images
   set wildignore+=*.swf                              " And another binary files.

   " disable sounds
   set noerrorbells
   set novisualbell
   set t_vb=

   " Searching
   set hlsearch
   set incsearch
   set ignorecase
   set smartcase
   set wrapscan

   " Enable spell checker for git commit
   autocmd FileType gitcommit set spell spelllang=en_us,pt_br textwidth=72

   " ---------------------------------------------------------------------------------------------------"
   " Tell vim to remember certain things when we exit
   "
   "  '10  :  marks will be remembered for up to 10 previously edited files
   "  "100 :  will save up to 100 lines for each register
   "  :20  :  up to 20 lines of command-line history will be remembered
   "  %    :  saves and restores the buffer list
   "  n... :  where to save the viminfo files
   " ---------------------------------------------------------------------------------------------------"
   set viminfo='10,\"100,:20,%,n~/.viminfo

" }

" Basic mappings {
   let mapleader=","
   let g:mapleader=","
   nnoremap ; :
   imap jk <ESC>
" }

" Custom Functions {
   " Strip trailing whitepaces
   function! <SID>StripTrailingWhitespaces()
      " Preparation: save last search, and cursor position.
      let _s=@/
      let l = line(".")
      let c = col(".")
      " Do the business:
      %s/\s\+$//e
      " Clean up: restore previous search history, and cursor position
      let @/=_s
      call cursor(l, c)
   endfunction
   autocmd BufWritePre *.* :call <SID>StripTrailingWhitespaces()

   " Ensure a dir exists
   function! EnsureDirExists(dir)
      let l:dir_path = expand(a:dir)
      if !isdirectory(l:dir_path)
        if exists("*mkdir")
           call mkdir(expand(l:dir_path),'p')
           echo "Created directory: " . l:dir_path
        else
           echo "Please create directory: " . l:dir_path
        endif
      endif
   endfunction

   " Toggle relativenumber and number
   function! ToggleNumbering()
      if &relativenumber
        set number
        set norelativenumber
      else
        set relativenumber
        set nonumber
      endif
   endfunc

   function! ResCur()
       if line("'\"") <= line("$")
           normal! g`"
           return 1
       endif
   endfunction

   " Set tabstop, softtabstop and shiftwidth to the same value
   command! -nargs=* Stab call Stab()
   function! Stab()
   let l:tabstop = 1 * input('set tabstop = softtabstop = shiftwidth = ')
   if l:tabstop > 0
       let &l:sts = l:tabstop
       let &l:ts = l:tabstop
       let &l:sw = l:tabstop
   endif
   call SummarizeTabs()
   endfunction

   function! SummarizeTabs()
   try
       echohl ModeMsg
       echon 'tabstop='.&l:ts
       echon ' shiftwidth='.&l:sw
       echon ' softtabstop='.&l:sts
       if &l:et
       echon ' expandtab'
       else
       echon ' noexpandtab'
       endif
   finally
       echohl None
   endtry
   endfunction
" }

" Vim Backup {
   set backupdir=~/.cache/vim/backup
   set directory=~/.cache/vim/swap
   set undodir=~/.cache/vim/undo

   set backup
   set swapfile

   call EnsureDirExists(&backupdir)
   call EnsureDirExists(&directory)
   call EnsureDirExists(&undodir)

   if has('persistent_undo')
      set undofile
      set undolevels=1000
      set undoreload=10000
   endif
" }

" Basic Mappings {
   nnoremap <C-a> ggvG$         " Select all
   inoremap <C-v> <ESC> "+gpa   " Ctrl + v compatilibility
   vnoremap <C-c> "+y           " Ctrl + c compatilibility
   vnoremap <C-x> "+x           " Ctrl + x compatilibility

   nmap <S-h> :bp <CR> " Move to the tab at the right
   nmap <S-l> :bn <CR> " Move to the tab at the left

   map <leader>fc /\v^[<\|=>]{7}( .*\|$)<CR> " Find merge conflict markers

   noremap <leader>n :call ToggleNumbering()<cr>

   " Sane regex
   nnoremap / /\v
   vnoremap / /\v
   nnoremap ? ?\v
   vnoremap ? ?\v
   nnoremap :s/ :s/\v

   " Auto center everything
   nnoremap <silent> n nzz
   nnoremap <silent> N Nzz
   nnoremap <silent> * *zz
   nnoremap <silent> # #zz
   nnoremap <silent> g* g*zz
   nnoremap <silent> g# g#zz
   nnoremap <silent> <C-o> <C-o>zz
   nnoremap <silent> <C-i> <C-i>zz

   " reselect visual block after indent
   vnoremap < <gv
   vnoremap > >gv

   augroup resCur
       autocmd!
       autocmd BufWinEnter * call ResCur()
   augroup END

   augroup reload_vimrc
       autocmd!
       autocmd BufWritePost $MYVIMRC source $MYVIMRC
   augroup END
" }

" Setting up vim-plug {
  call plug#begin('~/.vim/plugged')

   " Web {
      Plug 'othree/html5.vim', { 'for': 'html' }
      Plug 'gregsexton/MatchTag', { 'for': ['html', 'xml'] }
      Plug 'mattn/emmet-vim', { 'for' : ['html','css','sass','scss','less'] }
   " }

   " Javascript {
      Plug 'leshill/vim-json', { 'for': 'javascript' }
      Plug 'maksimr/vim-jsbeautify', { 'for': 'javascript' }
      Plug 'pangloss/vim-javascript', { 'for': 'javascript' }

      autocmd FileType javascript noremap <buffer>  <c-f> :call JsBeautify()<cr>
      autocmd BufWritePre *.{js} :call JsBeautify()
   " }

   " Lua {
      Plug 'xolox/vim-lua-ftplugin', { 'for': 'lua' }
   " }

   " PHP {
      Plug 'spf13/PIV', { 'for': 'php' }
      Plug 'stephpy/vim-php-cs-fixer', { 'for': 'php' }
      Plug 'shawncplus/phpcomplete.vim', { 'for': 'php' }
   " }

   " Golang {
      Plug 'fatih/vim-go', { 'for': 'go' }
      Plug 'nsf/gocode', { 'for': 'go' }

      " vim-go
      let g:go_def_mode = 'godef'
   " }

   " Nginx {
      Plug 'vim-scripts/nginx.vim'
   " }

   " Docker {
     Plug 'ekalinin/Dockerfile.vim'
   " }

   " Autocomplete {
      Plug 'Valloric/YouCompleteMe', {
        \ 'dir': '~/.vim/bundle/YouCompleteMe',
        \ 'do': './install.py --clang-completer --gocode-completer --tern-completer'
        \ }
      Plug 'sirver/ultisnips'
      Plug 'honza/vim-snippets'

      " YouCompleteMe
      let g:ycm_key_list_select_completion = ['<C-n>']
      let g:ycm_key_list_previous_completion = ['<C-p>']

      " YouCompleteMe setup for Go development
      set completeopt-=preview
      au FileType go set completeopt-=preview

      let g:UltiSnipsEditSplit="vertical"
      let g:UltiSnipsExpandTrigger="<tab>"
      let g:UltiSnipsJumpBackwardTrigger="<S-tab>"
      let g:UltiSnipsJumpBackwardTrigger="<c-z>"
      let g:UltiSnipsJumpForwardTrigger="<c-b>"
      let g:UltiSnipsJumpForwardTrigger="<tab>"
   " }

   " Editing {
      Plug 'airblade/vim-gitgutter'
      Plug 'jiangmiao/auto-pairs'
      Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all'  }
      Plug 'junegunn/fzf.vim'
      Plug 'sheerun/vim-polyglot'
      Plug 'tmhedberg/matchit'
      Plug 'tomtom/tcomment_vim'
      Plug 'tpope/vim-endwise'
      Plug 'tpope/vim-surround'

      " FZF mappings
      map <silent><space>pf :GFiles?<cr>
      map <silent><space>ff :Files<cr>
      map <silent><space>fa :Ag<cr>
      map <silent><space>fc :Colors<cr>
      map <silent><space>fl :BLines<cr>
      map <silent><space>fb :Buffers<cr>
   " }

   " Navigation {
      Plug 'kien/ctrlp.vim' | Plug 'tacahiroy/ctrlp-funky'
   " }

   " Misc {
     Plug 'mattn/webapi-vim' | Plug 'mattn/gist-vim', {  'on': 'Gist' }
     Plug 'nanotech/jellybeans.vim'
     Plug 'scrooloose/syntastic'
     Plug 'vim-airline/vim-airline'
     Plug 'vim-airline/vim-airline-themes'
     Plug 'xolox/vim-misc'

      " syntastic {
        let g:syntastic_error_symbol = '✗'
        let g:syntastic_style_error_symbol = '✠'
        let g:syntastic_warning_symbol = '∆'
        let g:syntastic_style_warning_symbol = '≈'
        let g:syntastic_enable_signs=1                         " mark syntax errors with :signs
        let g:syntastic_auto_jump=0                            " automatically jump to the error when saving the file
        let g:syntastic_auto_loc_list=0                        " show the error list automatically
        let g:syntastic_quiet_messages = {'level': 'warnings'} " don't care about warnings

        " go
        let g:syntastic_go_checkers = [
          \ 'gofmt',
          \ 'golint',
          \ 'govet',
          \ 'errcheck',
          \ 'gosimple',
          \ 'deadcode',
          \ 'dupl',
          \ ]
        let g:syntastic_mode_map = { 'mode': 'active', 'passive_filetypes': ['go']  }

        " javascript
        let g:syntastic_javascript_checkers = ['eslint']
        " let g:syntastic_javascript_eslint_args = ['--fix']

        " JSON
        au BufRead,BufNewFile *.json set filetype=json
        let g:syntastic_json_checkers = ['jsonlint']

        autocmd VimEnter * SyntasticCheck " Check the syntax when enter in the Vim
      " }

      " vim-airline
      let g:airline_theme = 'powerlineish'
      let g:airline#extensions#syntastic#enabled = 1
      let g:airline#extensions#branch#enabled = 1
      let g:airline#extensions#tabline#enabled = 1
      let g:airline#extensions#tagbar#enabled = 1
      let g:airline_skip_empty_sections = 1
      "

   " }

   " Typescript {
     Plug 'leafgarland/typescript-vim', { 'for': 'typescript' }
     Plug 'Quramy/tsuquyomi', { 'for': 'typescript' }
   " }

   " Add plugins to &runtimepath
   call plug#end()

" }

" Wrapping up {

  " Custom Bundle configuration
  for fpath in split(globpath('~/.vim/bundle-settings', '*.vim'), '\n')
    exec 'source' fpath
  endfor

  filetype plugin indent on
  syntax enable

  " UI Configuration {
    colorscheme jellybeans
    set nospell
    set t_Co=256
    set nofoldenable
    set showmatch
    set nohidden
    highlight clear SignColumn     " SignColumn should match background
    highlight clear LineNr         " Current line number row will have same background color in relative mode
    highlight LineNr ctermfg=237
    highlight normal ctermbg=none
    highlight nontext ctermbg=none
    set showmatch                  " automatically highlight matching braces/brackets/etc.
    set matchtime=2                " tens of a second to show matching parentheses
    set relativenumber
    set lazyredraw
    set laststatus=2
    set noshowmode
  " }
" }
